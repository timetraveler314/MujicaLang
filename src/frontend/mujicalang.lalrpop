grammar;

use crate::frontend::ast::*;
use crate::frontend::ty::*;

// Lexical
match {
    r"\s*" => {}, // skip whitespace
    _
}

pub Expr: InputASTExpr = {
    #[precedence(level="1")]
    IfExpr,
    #[precedence(level="1")]
    LetExpr,
    // Lambdas bind the weakest
    #[precedence(level="10")]
    "fun" <args: OptionallyTypedIdent+> "->" <body: Expr> => {
        // Currying
        args.into_iter().rfold(
            body,
            |acc, arg| InputASTExpr::Lambda {
                arg, // TODO: No type annotation in this grammar
                body: Box::new(acc),
                ret_ty: None, // TODO: No return type in this grammar
            },
        )
    },
    #[precedence(level="1")]
    ApplyExpr,
    #[precedence(level="2")] #[assoc(side="left")]
    <l:Expr> "*" <r:Expr> => curry_binop!(OpType::Mul, l, r),
    #[precedence(level="2")] #[assoc(side="left")]
    <l:Expr> "/" <r:Expr> => curry_binop!(OpType::Div, l, r),
    #[precedence(level="3")] #[assoc(side="left")]
    <l:Expr> "+" <r:Expr> => curry_binop!(OpType::Add, l, r),
    #[precedence(level="3")] #[assoc(side="left")]
    <l:Expr> "-" <r:Expr> => curry_binop!(OpType::Sub, l, r),
    #[precedence(level="4")] #[assoc(side="left")]
    <l:Expr> "==" <r:Expr> => curry_binop!(OpType::Eq, l, r),
}

IfExpr: InputASTExpr = {
    "if" <cond: Expr> "then" <then: Expr> "else" <else_: Expr> "end" => InputASTExpr::If {
        cond: Box::new(cond),
        then: Box::new(then),
        else_: Box::new(else_),
        ty: None, // TODO: No type annotation in this grammar
    },
}

LetExpr: InputASTExpr = {
    "let" <bind: Ident> "=" <value: Expr> "in" <body: Expr> "end" => InputASTExpr::Let {
        bind: (bind, None),
        value: Box::new(value),
        body: Box::new(body),
        ty: None,
    },
    "let" <bind: Ident> ":" <scheme: TypeScheme> "=" <value: Expr> "in" <body: Expr> "end" => InputASTExpr::Let {
        bind: (bind, Some(scheme)),
        value: Box::new(value),
        body: Box::new(body),
        ty: None,
    },
}

LambdaExpr: InputASTExpr = {
    "fun" <args: OptionallyTypedIdent+> "->" <body: Expr> => {
        // Currying
        args.into_iter().rfold(
            body,
            |acc, arg| InputASTExpr::Lambda {
                arg: arg,
                body: Box::new(acc),
                ret_ty: None, // TODO: No return type in this grammar
            },
        )
    }
}

ApplyExpr: InputASTExpr = {
    <callee: ApplyExpr> <arg: Atom> => InputASTExpr::Apply {
        func: Box::new(callee),
        args: Box::new(arg),
        ty: None
    },
    <atom: Atom> => atom,
}

Atom: InputASTExpr = {
    <ident: Ident> => InputASTExpr::Atom(ASTAtom::Var(ident), None),
    <num: Int> => InputASTExpr::Atom(ASTAtom::Int(num), None),
    "(" <expr: Expr> ")" => expr,
    "()" => InputASTExpr::Atom(ASTAtom::Unit, None),
    "true" => InputASTExpr::Atom(ASTAtom::Bool(true), None),
    "false" => InputASTExpr::Atom(ASTAtom::Bool(false), None),
}

OptionallyTypedIdent: (String, Option<Ty>) = {
    "(" <ident: Ident> ":" <typ: Ty> ")" => (ident, Some(typ)),
    <ident: Ident> => (ident, None),
}

TypeScheme: Scheme = {
    "forall" <vars: Ident+> "." <ty: Ty> => Scheme {
        vars: vars,
        constraints: vec![],
        ty: ty,
    },
    <ty: Ty> => Scheme {
        vars: vec![], // no quantified variables
        constraints: vec![], // no constraints
        ty,
    }
}

Ty: Ty = {
    <t1:SimpleTy> "->" <t2:Ty> => Ty::Arrow(Box::new(t1), Box::new(t2)),
    <t:SimpleTy> => t,
}

SimpleTy: Ty = {
    "Int" => Ty::Int,
    "Unit" => Ty::Unit,
    "Bool" => Ty::Bool,
    <v:Ident> => Ty::Mono(v),
    "(" <t:Ty> ")" => t,
}

Ident: String = {
    <s:r"[a-z_][a-zA-Z0-9_]*"> => s.to_string(),
};

Int: i32 = {
    <n:r"-?[0-9]+"> => i32::from_str_radix(n, 10).unwrap(),
};